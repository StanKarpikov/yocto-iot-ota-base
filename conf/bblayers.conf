# POKY_BBLAYERS_CONF_VERSION is increased each time build/conf/bblayers.conf
# changes incompatibly
POKY_BBLAYERS_CONF_VERSION = "2"

BBPATH = "${TOPDIR}"
BBFILES ?= ""

# Enable using:
# export JETSON_LAYERS_ENABLED=1
# export BB_ENV_PASSTHROUGH_ADDITIONS="JETSON_LAYERS_ENABLED"
JETSON_LAYERS_ENABLED ?= "1"
JETSON_LAYERS ?= " \
  ${TOPDIR}/meta-mender-community/meta-mender-tegra \
  ${TOPDIR}/tegra-demo-distro/layers/meta-virtualization \
  ${TOPDIR}/tegra-demo-distro/layers/meta-tegra-support \
  ${TOPDIR}/tegra-demo-distro/layers/meta-tegrademo \
  ${TOPDIR}/tegra-demo-distro/layers/meta-tegra \
  ${TOPDIR}/meta-jetson-mender-iot-base \
  "
BBMASK += "${@ ".*raspberrypi.*" if bb.utils.to_boolean(d.getVar('JETSON_LAYERS_ENABLED')) else "" }"

RASPBERRYPI_LAYERS_ENABLED ?= "0"
RASPBERRYPI_LAYERS ?= " \
  ${TOPDIR}/meta-raspberry \
  ${TOPDIR}/meta-raspberrypi-mender-iot-base \
  ${TOPDIR}/tegra-demo-distro/repos/poky/meta-poky \
  "
BBMASK += "${@ ".*jetson.*" if bb.utils.to_boolean(d.getVar('RASPBERRYPI_LAYERS_ENABLED')) else "" }"

BBLAYERS ?= " \
  ${TOPDIR}/tegra-demo-distro/layers/meta \
  ${TOPDIR}/tegra-demo-distro/layers/meta-oe \
  ${TOPDIR}/tegra-demo-distro/layers/meta-python \
  ${TOPDIR}/tegra-demo-distro/layers/meta-networking \
  ${TOPDIR}/tegra-demo-distro/layers/meta-filesystems \
  ${@bb.utils.contains('JETSON_LAYERS_ENABLED', '1', '${JETSON_LAYERS}', '', d)} \
  ${@bb.utils.contains('RASPBERRYPI_LAYERS_ENABLED', '1', '${RASPBERRYPI_LAYERS}', '', d)} \
  ${TOPDIR}/meta-mender-append \
  ${TOPDIR}/meta-mender/meta-mender-core \
  "